define

# Based on https://docs.oasis-open.org/cti/stix/v2.1/cs01/stix-v2.1-cs01.html#-disnqa06jm5

### 1 Base Entities ###

stix-entity sub entity,
    abstract;

stix-object sub stix-entity,
    owns stix-type,
    owns stix-id @key,
    owns custom-attribute,

    plays granular-marking:marking;

stix-core-object sub stix-object,
    owns spec-version,

    plays object-marking:marked,
    plays created-by:created,
    plays derivation:derived-from,
    plays derivation:deriving,
    plays duplicate-of:duplicated-object;


stix-domain-object sub stix-core-object,
    owns created,
    owns modified,
    owns revoked,
    owns labels,
    owns confidence,
    owns langs,

    # Rel
    plays sighting:sighting-of,
    plays external-references:referencing,

    # RRel
    plays kill-chain-phases:used,
    plays sighting:observed-data,
    plays external-references:referenced;


stix-cyber-observable-object sub stix-core-object,
    owns defanged,

    # Rel
    plays external-references:referencing,
    plays contains-ref:containing,

    # RRel
    plays external-references:referenced;

stix-sub-object sub stix-entity,
    owns created,
    owns modified,

    plays granular-marking:marking;


### 2 Data Types ###

external-reference sub stix-sub-object,
    owns source-name,
    owns description,
    owns url-link,
    owns external-id,

    plays hashes:hashes-owner,
    plays external-references:referenced;

kill-chain-phase sub stix-sub-object,
    owns kill-chain-name,
    owns kill-chain-phase-name,

    plays kill-chain-phases:using;

### 3 SDOs ###

attack-pattern sub stix-domain-object,
    owns name,
    owns description,
    owns aliases,

    # Rel
    plays delivers:delivering,
    plays targets:targeting,
    plays uses:using,

    # RRel
    plays indicates:indicated,
    plays uses:used,
    plays mitigates:mitigated;

campaign sub stix-domain-object,
    owns name,
    owns description,
    owns aliases,
    owns first-seen,
    owns last-seen,
    owns objective,

    # Rel
    plays targets:targeting,
    plays attributed-to:attributing,
    plays uses:using,
    plays compromises:compromising,
    plays originates-from:originating,

    # RRel
    plays indicates:indicated;

course-of-action sub stix-domain-object,
    owns name,
    owns description,
    owns action,

    # Rel
    plays investigates:investigating,
    plays mitigates:mitigating,
    plays remediates:remediating;

grouping sub stix-domain-object,
    owns name,
    owns description,
    owns context;



identity sub stix-domain-object,
    owns name,
    owns description,
    owns stix-role,
    owns identity-class,
    owns sector,
    owns contact-information,

    plays created-by:creator,

    # Rel
    plays located-at:locating,
    plays uses:using,

    # RRel
    plays uses:used,
    plays targets:targeted,
    plays attributed-to:attributed,
    plays impersonates:impersonated;

individual sub identity;
group sub identity;
system sub identity;
organization sub identity;
class sub identity;
id-unknown sub identity;

incident sub stix-domain-object,
    owns name,
    owns description;

indicator sub stix-domain-object,
    owns name,
    owns description,
    owns indicator-type,
    owns pattern,
    owns pattern-type,
    owns pattern-version,
    owns valid-from,
    owns valid-until,

    # Rel
    plays indicates:indicating,
    plays based-on:basing,

    # RRel
    plays investigates:investigated,
    plays mitigates:mitigated;

infrastructure sub stix-domain-object,
    owns name,
    owns description,
    owns infrastructure-types,
    owns aliases,
    owns first-seen,
    owns last-seen,

    # Rel
    plays delivers:delivering,
    plays uses:using,
    plays located-at:locating,
    plays communicates-with:communicating,
    plays consist-of:consisting,
    plays controls:controlling,
    plays have:having,
    plays hosts:hosting,

    # RRel
    plays controls:controlled,
    plays communicates-with:communicated,
    plays compromises:compromised,
    plays indicates:indicated,
    plays uses:used,
    plays targets:targeted,
    plays hosts:hosted,
    plays beacons-to:beaconed-to,
    plays exfiltrates-to:exfiltrated-to,
    plays ownerships:owned;

intrusion-set sub stix-domain-object,
    owns name,
    owns description,
    owns aliases,
    owns first-seen,
    owns last-seen,
    owns goals,
    owns sophistication,
    owns resource-level,
    owns primary-motivation,
    owns secondary-motivations,

    # Rel
    plays targets:targeting,
    plays uses:using,
    plays attributed-to:attributing,
    plays compromises:compromising,
    plays originates-from:originating,
    plays hosts:hosting,
    plays ownerships:owning,

    # RRel
    plays indicates:indicated,
    plays attributed-to:attributed,
    plays authored-by:authored;

malware sub stix-domain-object,
    owns name,
    owns description,
    owns malware-types,
    owns is-family,
    owns aliases,
    owns first-seen,
    owns last-seen,
    owns architecture-execution-envs,
    owns implementation-languages,
    owns capabilities,

    # Rel
    plays targets:targeting,
    plays uses:using,
    plays originates-from:originating,
    plays controls:controlling,
    plays hosts:hosting,
    plays authored-by:authoring,
    plays beacons-to:beaconing-to,
    plays exfiltrates-to:exfiltrating-to,
    plays downloads:downloading,
    plays drops:dropping,
    plays exploits:exploiting,
    plays variant-of:varianted-from,
    plays communicates-with:communicating,

    # RRel
    plays attributed-to:attributed,
    plays controls:controlled,
    plays remediates:remediated,
    plays mitigates:mitigated,
    plays uses:used,
    plays delivers:delivered,
    plays indicates:indicated,
    plays downloads:downloaded,
    plays drops:dropped,
    plays variant-of:varianted,
    plays characterizes:characterized,
    plays analysis-of:analysed,
    plays static-analysis-of:analysed,
    plays dynamic-analysis-of:analysed;

location sub stix-domain-object,
    owns name,
    owns description,
    owns latitude,
    owns longitude,
    owns precision,
    owns region,
    owns country,
    owns administrative-area,
    owns city,
    owns street-address,
    owns postal-code,

    # RRel
    plays targets:targeted,
    plays originates-from:originated,
    plays located-at:located;

malware-analysis sub stix-domain-object,
    owns product,
    owns version,
    owns configuration-version,
    owns module,
    owns analysis-engine-version,
    owns analysis-definition-version,
    owns submitted,
    owns analysis-started,
    owns analysis-ended,
    owns result-name,
    owns result,

    # Rel
    plays characterizes:characterizing,
    plays analysis-of:analysing,
    plays static-analysis-of:analysing,
    plays dynamic-analysis-of:analysing;

note sub stix-domain-object,
    owns note-abstract,
    owns content,
    owns authors;

opinion sub stix-domain-object,
    owns explanation,
    owns authors,
    owns opinion-enum;

observed-data sub stix-domain-object,
    owns first-observed,
    owns last-observed,
    owns number-observed,

    # RRel
    plays based-on:based,
    plays consist-of:consisted;

report sub stix-domain-object,
    owns name,
    owns description,
    owns report-type,
    owns published;

threat-actor sub stix-domain-object,
    owns name,
    owns description,
    owns aliases,
    owns stix-role,
    owns first-seen,
    owns last-seen,
    owns goals,
    owns resource-level,
    owns primary-motivation,
    owns secondary-motivations,
    owns sophistication,
    owns personal-characteristics,
    owns roles,
    owns threat-actor-types,

    # Rel
    plays targets:targeting,
    plays uses:using,
    plays attributed-to:attributing,
    plays compromises:compromising,
    plays located-at:locating,
    plays impersonates:impersonating,
    plays hosts:hosting,
    plays ownerships:owning,

    # RRel
    plays attributed-to:attributed,
    plays indicates:indicated,
    plays authored-by:authored;

tool sub stix-domain-object,
    owns name,
    owns description,
    owns tool-types,
    owns aliases,
    owns first-seen,
    owns last-seen,
    owns tool-version,

    # Rel
    plays delivers:delivering,
    plays targets:targeting,
    plays uses:using,
    plays have:having,
    plays drops:dropping,

    # RRel
    plays uses:used,
    plays indicates:indicated,
    plays mitigates:mitigated,
    plays hosts:hosted,
    plays downloads:downloaded,
    plays drops:dropped;

custom-object sub stix-domain-object,
    owns name,
    owns description,
    owns aliases,
    owns first-seen,
    owns last-seen,
    owns objective,

    # Rel
    plays delivers:delivering,
    plays targets:targeting,
    plays uses:using,

    # RRel
    plays uses:used,
    plays mitigates:mitigated;

vulnerability sub stix-domain-object,
    owns name,
    owns description,

    # RRel
    plays targets:targeted,
    plays mitigates:mitigated,
    plays remediates:remediated,
    plays have:had,
    plays exploits:exploited;

### 5 SROs ###
stix-core-relationship sub relation,
    # Required
    owns spec-version,
    owns stix-id @key,
    owns created,
    owns modified,
    owns stix-type,

    # Optional
    owns description,
    owns revoked,
    owns labels,
    owns confidence,
    owns langs,
    owns custom-attribute,

    relates source,
    relates target,

    plays created-by:created,
    plays granular-marking:marking,
    plays external-references:referenced;

delivers sub stix-core-relationship,
    relates delivering as source,
    relates delivered as target;

targets sub stix-core-relationship,
    relates targeting as source,
    relates targeted as target;

attributed-to sub stix-core-relationship,
    relates attributing as source,
    relates attributed as target;

uses sub stix-core-relationship,
    relates using as source,
    relates used as target;

indicates sub stix-core-relationship,
    relates indicating as source,
    relates indicated as target;

compromises sub stix-core-relationship,
    relates compromising as source,
    relates compromised as target;

originates-from sub stix-core-relationship,
    relates originating as source,
    relates originated as target;

investigates sub stix-core-relationship,
    relates investigating as source,
    relates investigated as target;

mitigates sub stix-core-relationship,
    relates mitigating as source,
    relates mitigated as target;

remediates sub stix-core-relationship,
    relates remediating as source,
    relates remediated as target;

located-at sub stix-core-relationship,
    relates locating as source,
    relates located as target;

impersonates sub stix-core-relationship,
    relates impersonating as source,
    relates impersonated as target;

based-on sub stix-core-relationship,
    relates basing as source,
    relates based as target;

communicates-with sub stix-core-relationship,
    relates communicating as source,
    relates communicated as target;

consist-of sub stix-core-relationship,
    relates consisting as source,
    relates consisted as target;

controls sub stix-core-relationship,
    relates controlling as source,
    relates controlled as target;

have sub stix-core-relationship,
    relates having as source,
    relates had as target;

hosts sub stix-core-relationship,
    relates hosting as source,
    relates hosted as target;

authored-by sub stix-core-relationship,
    relates authoring as source,
    relates authored as target;

beacons-to sub stix-core-relationship,
    relates beaconing-to as source,
    relates beaconed-to as target;

exfiltrates-to sub stix-core-relationship,
    relates exfiltrating-to as source,
    relates exfiltrated-to as target;

downloads sub stix-core-relationship,
    relates downloading as source,
    relates downloaded as target;

drops sub stix-core-relationship,
    relates dropping as source,
    relates dropped as target;

exploits sub stix-core-relationship,
    relates exploiting as source,
    relates exploited as target;

variant-of sub stix-core-relationship,
    relates varianted-from as source,
    relates varianted as target;

characterizes sub stix-core-relationship,
    relates characterizing as source,
    relates characterized as target;

analysis sub stix-core-relationship,
    relates analysing as source,
    relates analysed as target;

analysis-of sub analysis;
static-analysis-of sub analysis;
dynamic-analysis-of sub analysis;

ownerships sub stix-core-relationship,
    relates owning as source,
    relates owned as target;

ref sub relation,
    relates source,
    relates target;

from-ref sub ref;
sender-ref sub ref;
raw-email-ref sub ref;
body-raw-ref sub ref;
parent-directory-ref sub ref;
content-ref sub ref;
optional-header sub ref,
    relates from as source;
belongs-to-ref sub ref;
src-ref sub ref;
dst-ref sub ref;
src-payload-ref sub ref;
dst-payload-ref sub ref;
encapsulated-by-ref sub ref;
massage-body-data-ref sub ref;
creator-user-ref sub ref;
image-ref sub ref;
parent-ref sub ref;
message-body-data-ref sub ref,
    relates from as source;

derivation sub relation,
    relates derived-from,
    relates deriving;

duplicate-of sub relation,
    relates duplicated-object;


sighting sub stix-core-relationship,
    owns first-seen,
    owns last-seen,
    owns count,
    owns summary,

    relates sighting-of,
    relates observed-data;



### 6 SCOs ###

artifact sub stix-cyber-observable-object,
    owns mime-type,
    owns payload-bin,
    owns url-link,
    owns encryption-algorithm,
    owns decryption-key,

    # RRel
    plays body-raw-ref:target,
    plays raw-email-ref:target,
    plays content-ref:target,
    plays message-body-data-ref:target;

autonomous-system sub stix-cyber-observable-object,
    owns number,
    owns name,
    owns rir,

    # RRel
    plays belongs-to-refs:belonging;

directory sub stix-cyber-observable-object,
    owns path,
    owns path-enc,
    owns ctime,
    owns mtime,
    owns atime,

    # Rel
    plays contains-ref:contained,

    # RRel
    plays parent-directory-ref:target;

domain-name sub stix-cyber-observable-object,
    owns stix-value,

    # Rel
    plays resolves-to-refs:resolved,

    # RRel
    plays resolves-to-refs:resolving,
    plays communicates-with:communicated;


email-addr sub stix-cyber-observable-object,
    owns stix-value,
    owns display-name,

    # Rel
    plays belongs-to-ref:source,

    # RRel
    plays from-ref:target,
    plays sender-ref:target,
    plays to-refs:to,
    plays cc-refs:to,
    plays bcc-refs:to;

email-message sub stix-cyber-observable-object,
    owns is-multipart,
    owns date,
    owns content-type,
    owns message-id,
    owns subject,
    owns body,

    # Rel
    plays from-ref:source,
    plays sender-ref:source,
    plays to-refs:from,
    plays cc-refs:from,
    plays bcc-refs:from,
    plays received-lines:owner,
    plays body-multipart:to,
    plays raw-email-ref:source;


email-mime-part sub stix-sub-object,
    owns body,
    owns content-type,
    owns content-disposition,

    # Rel
    plays body-raw-ref:source,

    # RRel
    plays body-multipart:from;

file sub stix-cyber-observable-object,
    owns size,
    owns name,
    owns name-enc,
    owns magic-number-hex,
    owns mime-type,
    owns ctime,
    owns mtime,
    owns atime,

    # Rel
    plays hashes:hashes-owner,
    plays parent-directory-ref:source,
    plays contains-ref:contained,
    plays content-ref:source,

    # RRel
    plays downloads:downloaded,
    plays body-raw-ref:target,
    plays image-ref:target,
    plays service-dll-refs:to;

archive-ext sub file,
    owns comment;

ntfs-ext sub file,
    owns sid,

    # Rel
    plays alternate-data-streams:from;

alternate-data-stream sub file,

    # RRel
    plays alternate-data-streams:to;

pdf-ext sub stix-cyber-observable-object,
    owns version,
    owns is-optimized,
    owns pdfid0,
    owns pdfid1,

    # Rel
    plays document-info-dict:document-owner;

raster-image-ext sub file,
    owns image-height,
    owns image-width,
    owns bits-per-pixel,

    # Rel
    plays exif-tags:exif-owner;

windows-pebinary-ext sub file,
    owns pe-type,
    owns imphash,
    owns machine-hex,
    owns number-of-sections,
    owns time-date-stamp,
    owns pointer-to-symbol-table-hex,
    owns number-of-symbols,
    owns size-of-optional-header,
    owns characterstics-hex,

    # Rel
    plays optional-header:from,
    plays sections:from;

windows-pe-optional-header-type sub entity,
    owns magic-hex,
    owns major-linker-version,
    owns minor-linker-version,
    owns size-of-code,
    owns size-of-initialized-data,
    owns size-ofuninitialized-data,
    owns address-of-entry-point,
    owns base-of-code,
    owns base-of-data,
    owns image-base,
    owns section-alignment,
    owns file-alignment,
    owns major-os-version,
    owns minor-os-version,
    owns major-image-version,
    owns minor-image-version,
    owns major-subsystem-version,
    owns minor-subsystem-version,
    owns win32-version-value-hex,
    owns size-of-image,
    owns size-of-headers,
    owns checksum-hex,
    owns subsystem-hex,
    owns dll-characteristics-hex,
    owns size-of-stack-reserve,
    owns size-of-stack-commit,
    owns size-of-heap-reserve,
    owns size-of-heap-commit,
    owns loader-flags-hex,
    owns number-of-rva-and-sizes,

    # Rel
    plays hashes:hashes-owner,

    # RRel
    plays optional-header:target;

windows-pe-section sub entity,
    owns name,
    owns size,
    owns entropy,

    # Rel
    plays hashes:hashes-owner,

    # RRel
    plays sections:to;

ipv4-address sub stix-cyber-observable-object,
    owns stix-value,

    # Rel
    plays resolves-to-refs:resolved,
    plays belongs-to-refs:belonged,

    # RRel
    plays resolves-to-refs:resolving,
    plays communicates-with:communicated;



ipv6-address sub stix-cyber-observable-object,
    owns stix-value,

    # Rel
    plays resolves-to-refs:resolved,
    plays belongs-to-refs:belonged,

    # RRel
    plays resolves-to-refs:resolving,
    plays communicates-with:communicated;


mac-addr sub stix-cyber-observable-object,
    owns stix-value,

    # RRel
    plays resolves-to-refs:resolving;


mutex sub stix-cyber-observable-object,
    owns name;


network-traffic sub stix-cyber-observable-object,
    owns start,
    owns end,
    owns is-active,
    owns src-port,
    owns dst-port,

    # Rel
    plays src-ref:source,
    plays dst-ref:source,
    plays src-payload-ref:source,
    plays dst-payload-ref:source,
    plays encapsulated-by-ref:source,
    plays protocols:from,
    plays ipfix:ipfix-owner,
    plays encapsulates-refs:encapsulated,

    # RRel
    plays encapsulates-refs:encapsulating,
    plays encapsulated-by-ref:target,
    plays opened-connection-refs:opening;

http-request-ext sub network-traffic,
    owns request-method,
    owns request-value,
    owns request-version,
    owns message-body-length,

    # Rel
    plays request-header:header-owner,
    plays message-body-data-ref:from;

icmp-ext sub network-traffic,
    owns icmp-type-hex,
    owns icmp-code-hex;

socket-ext sub network-traffic,
    owns address-family,
    owns is-blocking,
    owns is-listening,
    owns socket-type,
    owns socket-descriptor,
    owns socket-handle,

    # Rel
    plays options:options-owner;

tcp-ext sub network-traffic,
    owns src-flags-hex,
    owns dst-flags-hex;

process sub stix-cyber-observable-object,
    owns is-hidden,
    owns pid,
    owns created-time,
    owns cwd,
    owns command-line,

    # Rel
    plays environment-variables:variables-owner,
    plays opened-connection-refs:opened,
    plays creator-user-ref:source,
    plays image-ref:source,
    plays parent-ref:source,
    plays child-refs:parent,

    # RRel
    plays parent-ref:target,
    plays child-refs:child;

windows-process-ext sub process,
    owns aslr-enabled,
    owns dep-enabled,
    owns priority,
    owns owner-sid,
    owns window-title,
    owns integrity-level,

    # Rel
    plays startup-info:info-owner;

windows-service-ext sub process,
    owns service-name,
    owns display-name,
    owns group-name,
    owns start-type,
    owns service-type,
    owns service-status,
    owns descriptions,

    # Rel
    plays service-dll-refs:from;

software sub stix-cyber-observable-object,
    owns name,
    owns spe,
    owns swid,
    owns vendor,
    owns version,
    owns languages;

url sub stix-cyber-observable-object,
    owns stix-value,

    # RRel
    plays communicates-with:communicated;


user-account sub stix-cyber-observable-object,
    owns user-id,
    owns credential,
    owns account-login,
    owns account-type,
    owns display-name,
    owns is-service-account,
    owns is-privileged,
    owns can-escalate-privs,
    owns is-disabled,
    owns account-created,
    owns account-expires,
    owns credential-last-changed,
    owns account-first-login,
    owns account-last-login,

    # RRel
    plays belongs-to-ref:target,
    plays creator-user-ref:target;

unix-account-ext sub user-account,
    owns gid,
    owns home-dir,
    owns shell,
    owns groups;


windows-registry-key sub stix-cyber-observable-object,
    owns attribute-key,
    owns modified-time,
    owns number-subkeys,

    # Rel
    plays values:from,
    plays creator-user-ref:source;

windows-registry-value-type sub stix-cyber-observable-object,
    owns name,
    owns data,
    owns data-type,

    # RRel
    plays values:to;

x509-certificate sub stix-cyber-observable-object,
    owns is-self-signed,
    owns version,
    owns serial-number,
    owns signature-algorithm,
    owns issuer,
    owns validity-not-before,
    owns validity-not-after,
    owns subject,
    owns subject-public-key-algorithm,
    owns subject-public-key-modulus,
    owns subject-public-key-exponent,

    # Rel
    plays hashes:hashes-owner;

x509-v3-extensions sub x509-certificate,
    owns basic-constraints,
    owns name-constraints,
    owns policy-constraints,
    owns key-usage,
    owns extended-key-usage,
    owns subject-key-identifier,
    owns authority-key-identifier,
    owns subject-alternative-name,
    owns issuer-alternative-name,
    owns subject-directory-attributes,
    owns crl-distribution-points,
    owns inhibit-any-policy,
    owns private-key-usage-period-not-before,
    owns private-key-usage-period-not-after,
    owns certificate-policies,
    owns policy-mappings;

marking-definition sub stix-object,
    owns created,
    owns modified,
    owns name,
    owns spec-version,

    plays created-by:created,
    plays data-marking:marking,
    plays external-references:referencing;

statement-marking sub marking-definition,
    owns statement;

tlp-marking sub marking-definition,
    owns color;

list sub relation,
    relates list-item,
    relates owner;

linked-list sub list,
    plays first-element:list,
    plays last-element:list,
    plays list-element:list;

first-element sub relation,
    relates first,
    relates list;

last-element sub relation,
    relates last,
    relates list;

list-element sub relation,
    relates element,
    relates list;

node sub relation,
    relates next,
    relates listed,

    # Rel
    plays node:next,

    # RRel
    plays list-element:element,
    plays last-element:last,
    plays first-element:first;

dict sub relation,
    relates dict-item,
    relates owner,
    owns key-abstract,
    abstract;


hashes sub dict,
    relates hash-value as dict-item,
    relates hashes-owner as owner,
    owns hash-algorithm as key-abstract;

document-info-dict sub dict,
    relates document-info as dict-item,
    relates document-owner as owner,
    owns key as key-abstract;

exif-tags sub dict,
    relates exif-tag as dict-item,
    relates exif-owner as owner,
    owns key as key-abstract;

ipfix sub dict,
    relates ipfix-field as dict-item,
    relates ipfix-owner as owner,
    owns key as key-abstract;

request-header sub dict,
    relates header as dict-item,
    relates header-owner as owner,
    owns key as key-abstract;

options sub dict,
    relates option as dict-item,
    relates options-owner as owner,
    owns key as key-abstract;

environment-variables sub dict,
    relates variable as dict-item,
    relates variables-owner as owner,
    owns key as key-abstract;

startup-info sub dict,
    relates info as dict-item,
    relates info-owner as owner,
    owns key as key-abstract;

additional-header-fields sub relation,
    relates dict-item,
    relates owner,

    plays header-fields:owner,

    owns key;

header-fields sub list,
    plays additional-header-fields:dict-item;

external-references sub list,
    relates referencing as list-item,
    relates referenced as owner;

kill-chain-phases sub list,
    relates using as list-item,
    relates used as owner;

data-marking sub list,
    relates marking as list-item,
    relates marked as owner,

    plays granular-marking:marking;

object-marking sub list,
    relates marking as list-item,
    relates marked as owner;

granular-marking sub list,
    relates marking as list-item,
    relates marked as owner;

created-by sub list,
    relates creator as list-item,
    relates created as owner;

resolves-to-refs sub list,
    relates resolving as list-item,
    relates resolved as owner;

belongs-to-refs sub list,
    relates belonging as list-item,
    relates belonged as owner;

contains-ref sub list,
    relates containing as list-item,
    relates contained as owner;

to-refs sub list,
    relates to as list-item,
    relates from as owner;

cc-refs sub list,
    relates to as list-item,
    relates from as owner;

bcc-refs sub list,
    relates to as list-item,
    relates from as owner;

body-multipart sub list,
    relates to as list-item,
    relates from as owner;

alternate-data-streams sub list,
    relates to as list-item,
    relates from as owner;

sections sub list,
    relates to as list-item,
    relates from as owner;

protocols sub list,
    relates to as list-item,
    relates from as owner;

encapsulates-refs sub list,
    relates encapsulating as list-item,
    relates encapsulated as owner;

opened-connection-refs sub list,
    relates opening as list-item,
    relates opened as owner;

child-refs sub list,
    relates child as list-item,
    relates parent as owner;

service-dll-refs sub list,
    relates to as list-item,
    relates from as owner;


values sub list,
    relates to as list-item,
    relates from as owner;

received-lines sub linked-list;

stix-attribute-string sub attribute,
    value string,

    plays granular-marking:marking,
    abstract;


stix-type sub stix-attribute-string;
stix-id sub stix-attribute-string;
stix-role sub stix-attribute-string;
spec-version sub stix-attribute-string;
labels sub stix-attribute-string;
langs sub stix-attribute-string;
defanged sub stix-attribute-string;
source-name sub stix-attribute-string;
url-link sub stix-attribute-string;
external-id sub stix-attribute-string;
name sub stix-attribute-string;
name-enc sub stix-attribute-string;
magic-number-hex sub stix-attribute-string;
mime-type sub stix-attribute-string;
aliases sub stix-attribute-string;
objective sub stix-attribute-string;
action sub stix-attribute-string;
context sub stix-attribute-string;
identity-class sub stix-attribute-string;
sector sub stix-attribute-string;
infrastructure-types sub stix-attribute-string;
contact-information sub stix-attribute-string;
indicator-type sub stix-attribute-string;
pattern sub stix-attribute-string;
pattern-type sub stix-attribute-string;
pattern-version sub stix-attribute-string;
goals sub stix-attribute-string;
resource-level sub stix-attribute-string;
primary-motivation sub stix-attribute-string;
secondary-motivations sub stix-attribute-string;
malware-types sub stix-attribute-string;
architecture-execution-envs sub stix-attribute-string;
implementation-languages sub stix-attribute-string;
capabilities sub stix-attribute-string;
region sub stix-attribute-string;
country sub stix-attribute-string;
administrative-area sub stix-attribute-string;
city sub stix-attribute-string;
street-address sub stix-attribute-string;
postal-code sub stix-attribute-string;
version sub stix-attribute-string;
configuration-version sub stix-attribute-string;
module sub stix-attribute-string;
analysis-engine-version sub stix-attribute-string;
analysis-definition-version sub stix-attribute-string;
result-name sub stix-attribute-string;
result sub stix-attribute-string;
note-abstract sub stix-attribute-string;
content sub stix-attribute-string;
authors sub stix-attribute-string;
explanation sub stix-attribute-string;
opinion-enum sub stix-attribute-string;
report-type sub stix-attribute-string;
sophistication sub stix-attribute-string;
personal-characteristics sub stix-attribute-string;
roles sub stix-attribute-string;
threat-actor-types sub stix-attribute-string;
tool-types sub stix-attribute-string;
tool-version sub stix-attribute-string;
vulnerability-types sub stix-attribute-string;
kill-chain-name sub stix-attribute-string;
kill-chain-phase-name sub stix-attribute-string;
summary sub stix-attribute-string;
payload-bin sub stix-attribute-string;
decryption-key sub stix-attribute-string;
path sub stix-attribute-string;
path-enc sub stix-attribute-string;
rir sub stix-attribute-string;
display-name sub stix-attribute-string;
content-type sub stix-attribute-string;
message-id sub stix-attribute-string;
subject sub stix-attribute-string;
body sub stix-attribute-string;
content-disposition sub stix-attribute-string;
comment sub stix-attribute-string;
sid sub stix-attribute-string;
owner-sid sub stix-attribute-string;
pdfid0 sub stix-attribute-string;
pdfid1 sub stix-attribute-string;
pe-type sub stix-attribute-string;
imphash sub stix-attribute-string;
machine-hex sub stix-attribute-string;
pointer-to-symbol-table-hex sub stix-attribute-string;
characterstics-hex sub stix-attribute-string;
win32-version-value-hex sub stix-attribute-string;
checksum-hex sub stix-attribute-string;
subsystem-hex sub stix-attribute-string;
dll-characteristics-hex sub stix-attribute-string;
loader-flags-hex sub stix-attribute-string;
magic-hex sub stix-attribute-string;
request-method sub stix-attribute-string;
request-value sub stix-attribute-string;
request-version sub stix-attribute-string;
icmp-type-hex sub stix-attribute-string;
icmp-code-hex sub stix-attribute-string;
service-name sub stix-attribute-string;
subject-public-key-algorithm sub stix-attribute-string;
subject-public-key-modulus sub stix-attribute-string;
certificate-policies sub stix-attribute-string;
crl-distribution-points sub stix-attribute-string;
subject-directory-attributes sub stix-attribute-string;
key-usage sub stix-attribute-string;
subject-alternative-name sub stix-attribute-string;
subject-key-identifier sub stix-attribute-string;
extended-key-usage sub stix-attribute-string;
name-constraints sub stix-attribute-string;
policy-mappings sub stix-attribute-string;
policy-constraints sub stix-attribute-string;
basic-constraints sub stix-attribute-string;
inhibit-any-policy sub stix-attribute-string;
authority-key-identifier sub stix-attribute-string;
issuer-alternative-name sub stix-attribute-string;
data sub stix-attribute-string;
user-id sub stix-attribute-string;
priority sub stix-attribute-string;
stix-value sub stix-attribute-string;
cwd sub stix-attribute-string;
command-line sub stix-attribute-string;
account-login sub stix-attribute-string;
group-name sub stix-attribute-string;
dst-flags-hex sub stix-attribute-string;
src-flags-hex sub stix-attribute-string;
product sub stix-attribute-string;
spe sub stix-attribute-string;
exif-tag-string sub stix-attribute-string;
window-title sub stix-attribute-string;
statement sub stix-attribute-string;
home-dir sub stix-attribute-string;
account-type sub stix-attribute-string;
credential sub stix-attribute-string;
attribute-key sub stix-attribute-string;
shell sub stix-attribute-string;
swid sub stix-attribute-string;
vendor sub stix-attribute-string;
description sub stix-attribute-string;
descriptions sub stix-attribute-string;
languages sub stix-attribute-string;
groups sub stix-attribute-string;

received sub stix-attribute-string,
    plays node:listed;
document-info sub stix-attribute-string,
    plays document-info-dict:document-info;
protocol sub stix-attribute-string,
    plays protocols:to;
ipfix-string sub stix-attribute-string,
    plays ipfix:ipfix-field;
header sub stix-attribute-string,
    plays request-header:header;
environment-variable sub stix-attribute-string,
    plays environment-variables:variable;
startup sub stix-attribute-string,
    plays startup-info:info;

issuer sub stix-attribute-string;
serial-number sub stix-attribute-string;
signature-algorithm sub stix-attribute-string;
subject-public-key-exponent sub stix-attribute-string;

hash-value sub stix-attribute-string,
    plays hashes:hash-value;
key-abstract sub stix-attribute-string,
    abstract;
key sub key-abstract;
hash-algorithm sub key-abstract,
    regex "^(MD5|SHA-1|SHA-256|SHA-512|SHA3-256|SHA3-512|SSDEEP|TLSH)$";
encryption-algorithm sub stix-attribute-string,
    regex "^(AES-256-GCM|ChaCha20-Poly1305|mime-type-indicated)$";
address-family sub stix-attribute-string,
    regex "^(AF-UNSPEC|AF-INET|AF-IPX|AF-APPLETALK|AF-NETBIOS|AF-INET6|AF-IRDA|AF-BTH)$";
socket-type sub stix-attribute-string,
    regex "^(SOCK-STREAM|SOCK-DGRAM|SOCK-RAW|SOCK-RDM|SOCK-SEQPACKET)$";
opinion-enum sub stix-attribute-string,
    regex "^(strongly-disagree|disagree|neutral|agree|strongly-agree)$";
integrity-level sub stix-attribute-string,
    regex "^(low|medium|high|system)$";
data-type sub stix-attribute-string,
    regex "^(REG-NONE|REG-SZ|REG-EXPAND-SZ|REG-BINARY|REG-DWORD|REG-DWORD-BIG-ENDIAN|REG-LINK|REG-MULTI-SZ|REG-RESOURCE-LIST|REG-FULL-RESOURCE-DESCRIPTION|REG-RESOURCE-REQUIREMENTS-LIST|REG-QWORD|REG-INVALID-TYPE)$";
start-type sub stix-attribute-string,
    regex "^(SERVICE-AUTO-START|SERVICE-BOOT-START|SERVICE-DEMAND-START|SERVICE-DISABLED|SERVICE-SYSTEM-ALERT)$";
service-type sub stix-attribute-string,
    regex "^(SERVICE-KERNEL-DRIVER|SERVICE-FILE-SYSTEM-DRIVER|SERVICE-WIN32-OWN-PROCESS|SERVICE-WIN32-SHARE-PROCESS)$";
service-status sub stix-attribute-string,
    regex "^(SERVICE-CONTINUE-PENDING|SERVICE-PAUSE-PENDING|SERVICE-PAUSED|SERVICE-RUNNING|SERVICE-START-PENDING|SERVICE-STOP-PENDING|SERVICE-STOPPED)$";
color sub stix-attribute-string,
    regex "^(white|green|amber|red|clear)$";
stix-attribute-double sub attribute,
    value double,

    plays granular-marking:marked,
    abstract;

number sub stix-attribute-double;
latitude sub stix-attribute-double;
longitude sub stix-attribute-double;
precision sub stix-attribute-double;
number-observed sub stix-attribute-double;
count sub stix-attribute-double;
entropy sub stix-attribute-double;
size-ofuninitialized-data sub stix-attribute-double;

stix-attribute-integer sub attribute,
    value long,

    plays granular-marking:marked,
    abstract;

size sub stix-attribute-integer;
gid sub stix-attribute-integer;
image-height sub stix-attribute-integer;
image-width sub stix-attribute-integer;
bits-per-pixel sub stix-attribute-integer;
confidence sub stix-attribute-integer;
number-of-sections sub stix-attribute-integer;
number-of-symbols sub stix-attribute-integer;
size-of-optional-header sub stix-attribute-integer;
major-linker-version sub stix-attribute-integer;
minor-linker-version sub stix-attribute-integer;
size-of-code sub stix-attribute-integer;
size-of-initialized-data sub stix-attribute-integer;
size-of-uninitialized-data sub stix-attribute-integer;
address-of-entry-point sub stix-attribute-integer;
base-of-code sub stix-attribute-integer;
base-of-data sub stix-attribute-integer;
image-base sub stix-attribute-integer;
section-alignment sub stix-attribute-integer;
file-alignment sub stix-attribute-integer;
major-os-version sub stix-attribute-integer;
minor-os-version sub stix-attribute-integer;
major-image-version sub stix-attribute-integer;
minor-image-version sub stix-attribute-integer;
major-subsystem-version sub stix-attribute-integer;
minor-subsystem-version sub stix-attribute-integer;
size-of-image sub stix-attribute-integer;
size-of-headers sub stix-attribute-integer;
size-of-stack-reserve sub stix-attribute-integer;
size-of-stack-commit sub stix-attribute-integer;
size-of-heap-reserve sub stix-attribute-integer;
size-of-heap-commit sub stix-attribute-integer;
number-of-rva-and-sizes sub stix-attribute-integer;
message-body-length sub stix-attribute-integer;
number-subkeys sub stix-attribute-integer;
exif-tag-int sub stix-attribute-integer;
src-port sub stix-attribute-integer;
dst-port sub stix-attribute-integer;
src-byte-count sub stix-attribute-integer;
dst-byte-count sub stix-attribute-integer;
src-packets sub stix-attribute-integer;
dst-packets sub stix-attribute-integer;
socket-descriptor sub stix-attribute-integer;
socket-handle sub stix-attribute-integer;
pid sub stix-attribute-integer;
option sub stix-attribute-integer,
    plays options:option;
ipfix-integer sub stix-attribute-integer,
    plays ipfix:ipfix-field;

stix-attribute-boolean sub attribute,
    value boolean,

    plays granular-marking:marked,
    abstract;

is-family sub stix-attribute-boolean;
is-optimized sub stix-attribute-boolean;
is-self-signed sub stix-attribute-boolean;
dep-enabled sub stix-attribute-boolean;
is-active sub stix-attribute-boolean;
is-hidden sub stix-attribute-boolean;
is-blocking sub stix-attribute-boolean;
is-listening sub stix-attribute-boolean;
can-escalate-privs sub stix-attribute-boolean;
is-service-account sub stix-attribute-boolean;
is-privileged sub stix-attribute-boolean;
can-escalate-privs sub stix-attribute-boolean;
is-disabled sub stix-attribute-boolean;
is-multipart sub stix-attribute-boolean;
aslr-enabled sub stix-attribute-boolean;
revoked sub stix-attribute-boolean;

stix-attribute-timestamp sub attribute,
    value datetime,

    plays granular-marking:marked,
    abstract;

date sub stix-attribute-timestamp;
ctime sub stix-attribute-timestamp;
atime sub stix-attribute-timestamp;
mtime sub stix-attribute-timestamp;
created sub stix-attribute-timestamp;
modified sub stix-attribute-timestamp;
submitted sub stix-attribute-timestamp;
valid-from sub stix-attribute-timestamp;
valid-until sub stix-attribute-timestamp;
first-observed sub stix-attribute-timestamp;
last-observed sub stix-attribute-timestamp;
analysis-started sub stix-attribute-timestamp;
analysis-ended sub stix-attribute-timestamp;
published sub stix-attribute-timestamp;
first-seen sub stix-attribute-timestamp;
last-seen sub stix-attribute-timestamp;
time-date-stamp sub stix-attribute-timestamp;
end sub stix-attribute-timestamp;
start sub stix-attribute-timestamp;
created-time sub stix-attribute-timestamp;
modified-time sub stix-attribute-timestamp;
account-created sub stix-attribute-timestamp;
account-expires sub stix-attribute-timestamp;
credential-last-changed sub stix-attribute-timestamp;
account-first-login sub stix-attribute-timestamp;
account-last-login sub stix-attribute-timestamp;
validity-not-before sub stix-attribute-timestamp;
validity-not-after sub stix-attribute-timestamp;
private-key-usage-period-not-after sub stix-attribute-timestamp;
private-key-usage-period-not-before sub stix-attribute-timestamp;

custom-attribute sub attribute, value string,
    plays granular-marking:marked,
    owns attribute-type;

attribute-type sub attribute, value string;

rule linked-list-item:
when {
    { (list: $x, first: $z) isa first-element;}
    or {
        (list: $x, element: $y) isa list-element;
        $y (next: $z) isa node;
    };
    $x isa linked-list;
    $z isa node;
    $y isa node;
} then {
    (list: $x, element: $z) isa list-element;
};

rule last-element-linked-list:
when {
    (list: $x, element: $y) isa list-element;
    not {
        $z isa node;
        $y (next: $z);
    };
    $x isa linked-list;
    $y isa node;
    $z isa node;
} then {
    (list: $x, last: $y) isa last-element;
};

rule transitive-use:
when {
    $x isa stix-domain-object;
    $y isa stix-domain-object;
    $z isa stix-domain-object;
    $use1 (using: $x, used: $y) isa uses;
    $use2 (using: $y, used: $z) isa uses;
} then {
    (using: $x, used: $z) isa uses;
};

rule attributed-to-when-using:
when {
    (attributing: $z, attributed: $x) isa attributed-to;
    (using: $x, used: $y) isa uses;
} then {
    (using: $z, used: $y) isa uses;
};

rule attributed-to-when-targeting:
when {
    (attributing: $z, attributed: $x) isa attributed-to;
    (targeting: $x, targeted: $y) isa targets;
} then {
    (targeting: $z, targeted: $y) isa targets;
};